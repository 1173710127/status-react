pipeline {
  agent { 
    docker {
      label 'linux'
      image 'statusteam/status-build-android:1.0.0'
      args (
        "-v /home/jenkins/tmp:/var/tmp:rw "+
        "-v /home/jenkins/status-im.keystore:/tmp/status-im.keystore:ro"
      )
    }
  }

  options {
    timestamps()
    /* Prevent Jenkins jobs from running forever */
    timeout(time: 35, unit: 'MINUTES')
    /* Limit builds retained */
    buildDiscarder(logRotator(
      numToKeepStr: '90',
      daysToKeepStr: '30',
      artifactNumToKeepStr: '90',
    ))
  }

  environment {
    BUILD_PLATFORM = 'android'
    LANG     = 'en_US.UTF-8'
    LANGUAGE = 'en_US.UTF-8'
    LC_ALL   = 'en_US.UTF-8'
    FASTLANE_DISABLE_COLORS = 1
    REALM_DISABLE_ANALYTICS = 1
    /* since we are mounting it we need to specify location */
    STATUS_RELEASE_STORE_FILE = '/tmp/status-im.keystore'
    ANDROID_HOME      = '/usr/lib/android-sdk'
    ANDROID_SDK_ROOT  = '/usr/lib/android-sdk'
    ANDROID_NDK       = '/usr/lib/android-ndk'
    ANDROID_NDK_HOME  = '/usr/lib/android-ndk'
    /* We use EXECUTOR_NUMBER to avoid multiple instances clashing */
    LEIN_HOME         = "/var/tmp/lein-${EXECUTOR_NUMBER}"
    YARN_CACHE_FOLDER = "/var/tmp/yarn-${EXECUTOR_NUMBER}"
    BUNDLE_PATH       = "/var/tmp/bundle-${EXECUTOR_NUMBER}"
    GRADLE_USER_HOME  = "/var/tmp/gradle-${EXECUTOR_NUMBER}"
  }

  stages {
    stage('Prep') {
      steps {
        script {
          print "Running ${params.BUILD_TYPE} build!"
          /* Necessary to load methods */
          mobile = load 'ci/mobile.groovy'
          cmn    = load 'ci/common.groovy'
          /* Cleanup and Prep */
          mobile.prep(cmn.getBuildType())
          /* Run at start to void mismatched numbers */
          cmn.buildNumber()
        }
      }
    }
    stage('Lint') {
      steps {
        script { cmn.runLint() }
      }
    }
    stage('Tests') {
      steps {
        script { cmn.runTests() }
      }
    }
    stage('Build') {
      steps {
        script { mobile.leinBuild('android') }
      }
    }
    stage('Compile') {
      steps {
        script { apk = mobile.android.compile(cmn.getBuildType()) }
      }
    }
    stage('Archive') {
      steps {
        script { archiveArtifacts apk }
      }
    }
    stage('Upload') {
      steps {
        script {
          switch (cmn.getBuildType()) {
            case 'release':
              mobile.android.uploadToPlayStore(); break;
            case 'nightly':
              env.DIAWI_URL = mobile.android.uploadToDiawi(); break;
            case 'e2e':
              env.SAUCE_URL = mobile.android.uploadToSauceLabs()
          }
        }
      }
    }
  }
}
